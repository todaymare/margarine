extern "https://github.com/todaymare/margarine-std" as std;
use std::*;
use std::io::read_file;
use std::iter::Iter;

fn main() {
    var input = read_file("./examples/aoc2025/day1.txt");

    println(part1(input));
}


fn part1(input: str): str {
    input
        .split(",")
        .filter(|n| !n.is_empty())
        .map(|range| {
            var a, b = range.split_once("-")!;
            var start : int = a.parse()!;
            var end : int = b.parse()!;

            (start..=end)
                .iter()
                .filter(|n| is_repeated(n))
                .sum()
        })
        .sum()
        .to_str()
}


fn is_repeated(n: int): bool {
    var s = n.to_str();
    if s.len() % 2 != 0 { return false }

    var mid = s.len() / 2;
    var left, right = s.split_at(mid);
    left == right
}


impl<T> Iter<T> {
    fn filter(self, cond: fn(T): bool): Iter<T> {
        Iter {
            next: || {
                loop {
                    var value = self.__next__()?;
                    if cond(value) {
                        return some(value)
                    }
                }

                none()
            }
        }
    }
}


impl Iter<int> {
    fn sum(self): int {
        var sum = (0,);
        self.for_each(|n| { sum.0 += n });
        sum.0
    }
}


impl Range {
    fn iter(self): Iter<int> {
        Iter {
            next: || self.__next__(),
        }
    }
}
