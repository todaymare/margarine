extern "https://github.com/todaymare/margarine-std" as std;
use std::*;
use std::io::read_file;

fn main() {
    var input = read_file("./examples/aoc2025/day0.txt");

    println(part1(input));
    println(part2(input));
}


fn part1(input: str): str {
    var dial, counter = (50, 0);

    for instr in input.lines() {
        if instr.is_empty() { continue }

        var dir, value = instr.split_at(1);
        var value = value.parse::<int>()!;

        var delta =
        if dir == "L" { -value }
        else { value };

        dial += delta;
        dial = ((dial % 100) + 100) % 100;

        if dial == 0 {
            counter += 1;
        }
    }

    counter.to_str()
}


fn part2(input: str): str {
    var dial, counter = (50, 0);

    for instr in input.lines() {
        if instr.is_empty() { continue }

        var dir, value = instr.split_at(1);
        var value = value.parse::<int>()!;

        if dir == "R" {
            counter += (dial + value) / 100;
            dial = ((dial + value) % 100 + 100) % 100;
        } else {
            var t0 = if dial == 0 { 100 } else { dial };
            if value >= t0 {
                counter += 1 + (value - t0) / 100;
            }

            dial = ((dial - value) % 100 + 100) % 100;
        }
    }

    counter.to_str()
}


